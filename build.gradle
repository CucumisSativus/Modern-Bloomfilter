import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.30.0"
    }
}

plugins {
    id 'net.researchgate.release' version '2.6.0'
    id "com.vanniktech.maven.publish" version "0.35.0"
}

apply plugin: 'java'
apply plugin: 'io.codearte.nexus-staging'

repositories {
    mavenCentral()
}

dependencies {
    implementation(
            'com.google.code.gson:gson:2.8.9',
            "redis.clients:jedis:2.9.0",
            //"org.apache.commons:commons-pool2:2.2",
            "org.slf4j:slf4j-api:$slf4jVersion",
            "org.msgpack:msgpack:0.6.12",
    )
    testImplementation(
            'junit:junit:4.13.1',
            "org.apache.commons:commons-math:2.2",
            "com.google.guava:guava:33.4.0-jre",
            'org.apache.commons:commons-lang3:3.18.0',
            "io.dropwizard.metrics:metrics-core:3.2.3",
            "org.slf4j:slf4j-simple:$slf4jVersion",
            "org.testcontainers:testcontainers:2.0.2",
            "com.redis:testcontainers-redis:2.2.2"
    )
}

group = "io.github.cucumissativus"
// Set the base archive name in a Gradle 9 compatible way
base {
    archivesName = "bloom-filter"
}

wrapper {
    // Keep aligned with the wrapper in gradle-wrapper.properties if the wrapper task is run
    gradleVersion = '9.2.1'
}

tasks.register('javadocJar', Jar) {
    from tasks.named('javadoc')
    archiveClassifier = 'javadoc'
}

tasks.register('sourcesJar', Jar) {
    from sourceSets.main.allSource
    archiveClassifier = 'sources'
}


test {
    ignoreFailures = project.hasProperty("test.ignoreFailures") ? project["test.ignoreFailures"] : false
}

mavenPublishing {
    coordinates(group.toString(), name.toString(), version.toString())

    pom {
        name = 'Bloom filter'
        description = 'A bloom filter implementation in java'
        url = 'https://github.com/CucumisSativus/Modern-Bloomfilter'
        licenses {
            license {
                name = 'MIT'
                url = 'https://github.com/CucumisSativus/Modern-Bloomfilter/blob/master/LICENSE'
            }
        }
        developers {
            developer {
                id = 'CucumisSativus'
                name = 'CucumisSativus'
                url = "https://github.com/CucumisSativus"
            }
        }
        scm {
            connection = 'scm:git:git://example.com/my-library.git'
            developerConnection = 'scm:git:git://github.com:CucumisSativus/Modern-Bloomfilter.git'
            url = 'scm:git:git://github.com:CucumisSativus/Modern-Bloomfilter.git'
        }
    }
}


release {
    preTagCommitMessage = "[ci skip] pre tag commit:"
    tagCommitMessage = "creating tag:"
    newVersionCommitMessage = "[ci skip] new version commit:"
    buildTasks = ['assemble']
}

// Use the modern publish task instead of removed uploadArchives
afterReleaseBuild.dependsOn tasks.named('publish')

signing {
    // Sign the Maven publication instead of the removed 'archives' configuration
    useGpgCmd()
    sign publishing.publications
}

